%{

#include "drbd_endian.h"
#include "drbdmeta_parser.h"

static void unescape(void);

//#define DP printf("%s ",yytext);
#define DP
#define YY_NO_UNPUT 1
static void yyunput (int c, register char * yy_bp ) __attribute((unused));

%}

%option noyywrap

WS		[ \t\n]
COMMENT		\#[^\n]*
NUM		[0-9]{1,10}
U64		0x[0-9A-Fa-f]{16}
U32		0x[0-9A-Fa-f]{8}
OP		[{};]
STRING		\"[^\"]*\"

%%

{WS}
{COMMENT}
{OP}		DP; return yytext[0];
{STRING}	unescape(); yylval.txt=yytext; DP; return TK_STRING; 
{U64}		yylval.u64 = strto_u64(yytext, NULL, 16); DP; return TK_U64;
{U32}		yylval.u64 = strto_u64(yytext, NULL, 16); DP; return TK_U32;
{NUM}		yylval.u64 = strto_u64(yytext, NULL, 10); DP; return TK_NUM;
gc		DP; return TK_GC;
bm		DP; return TK_BM;
uuid		DP; return TK_UUID;
version		DP; return TK_VERSION;
la-size-sect	DP; return TK_LA_SIZE;
bm-byte-per-bit DP; return TK_BM_BYTE_PER_BIT;
device-uuid	DP; return TK_DEVICE_UUID;
times		DP; return TK_TIMES;
flags		DP; return TK_FLAGS;

%%

static void unescape(void)
{
  /* backslash escapes from string */
  char *ue, *e;
  e = ue = yytext;
  for (;;) {
    if (*ue == '"')
      ue++;
    if (*ue == '\\')
      ue++;
    if (!*ue)
      break;
    *e++ = *ue++;
  }
  *e = '\0';
}
